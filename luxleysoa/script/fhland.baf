//CONTENTS//

//Greetings
//Transformation script
//Parting script
//Leaving when Sebastian is permanently dead
//Talk timers
//Quest script and talk delays
//Making the game recognise when S and A are rowing
//ESTM script
//String fixer

//Initial greeting
IF
Global("FHLMeetLux","GLOBAL",0)
Range([PC],8)
THEN
RESPONSE #100
StartDialogNoSet("FHLCRIM1")
END

IF
Global("FHLMeetLux","GLOBAL",1)
!Dead("FHLCRIM1")
THEN
RESPONSE #100
Attack("FHLCRIM1")
END

IF
Global("FHLMeetLux","GLOBAL",1)
Dead("FHLCRIM1")
THEN
RESPONSE #100
StartDialogNoSet(Player1)
END

IF
Global("FHLMeetLux","GLOBAL",2)
THEN
RESPONSE #100
StartDialogNoSet(Player1)
END

IF
Global("FHLLuxMove","GLOBAL",1)
Global("FHLAndMove","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("FHLAndMove","GLOBAL",1)
EscapeAreaMove("AR0700",2169,2452,0)
END

//Andrei's transformation
IF
HPPercentLT(Myself,20)
Global("FHLBearChange","LOCALS",0)
!Dead("FHLXAV")
THEN
RESPONSE #100
SetGlobal("FHLBearChange","LOCALS",1)
SetGlobalTimer("FHLAndreiChange","GLOBAL",90)
ApplySpellRES("FHLAND",MYSELF)
END

IF
Global("FHLBearChange","LOCALS",1)
GlobalTimerExpired("FHLAndreiChange","GLOBAL")
THEN
RESPONSE #100
SetGlobal("FHLBearChange","LOCALS",0)
ApplySpellRES("FHL491",MYSELF)
END

IF
Global("FHLBearChange","LOCALS",1)
Dead("FHLXAV")
THEN
RESPONSE #100
SetGlobal("FHLBearChange","LOCALS",0)
ApplySpellRES("FHL491",MYSELF)
END

//Ensure Andrei leaves when Sebastian does

IF
InParty("FHLAND")
!Dead("FHLSEB")
!InParty("FHLSEB")
Global("FHLReturns","GLOBAL",0)
Global("FHLKicked","GLOBAL",1)
Global("FHLJoined","GLOBAL",0)
CombatCounter(0)
THEN
RESPONSE #100
LeaveParty()
SetLeavePartyDialogFile()
ChangeAIScript("",DEFAULT)
EscapeAreaMove("AR0700",2169,2452,0)
END

IF
InParty("FHLAND")
!Dead("FHLSEB")
!InParty("FHLSEB")
Global("FHLReturns","GLOBAL",0)
Global("FHLKicked","GLOBAL",2)
Global("FHLJoined","GLOBAL",0)
CombatCounter(0)
THEN
RESPONSE #100
LeaveParty()
SetLeavePartyDialogFile()
ChangeAIScript("",DEFAULT)
END

IF
Global("FHLReturns","GLOBAL",0)
Global("FHLKicked","GLOBAL",3)
InParty(Myself)
THEN
RESPONSE #100
LeaveParty()
EscapeArea()
END

//Ensure Andrei leaves when Sebastian is permanently dead

IF
InParty("FHLAND")
Dead("FHLSEB")
!InPartyAllowDead("FHLSEB")
Global("FHLJoined","GLOBAL",1)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("FHLSebDead","GLOBAL",1)
StartDialogueNoSet(Player1)
END


//TALK TIMERS
//Setting initial talk timer
IF
InParty(Myself)
Global("FHLAndSebFirstTalk","GLOBAL",2)
Global("FHLAndTalk","GLOBAL",0)
Global("FHLSebTalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("FHLAndTalk","GLOBAL",1)
SetGlobal("FHLSebTalk","GLOBAL",1)
RealSetGlobalTimer("FHLLuxTalkTime","GLOBAL",TWELVE_HOURS)
END

//Initiating talks
IF
InParty(Myself)
Global("FHLLuxTalk","GLOBAL",0)
RealGlobalTimerExpired("FHLLuxTalkTime","GLOBAL")
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
!Global("FHLAndSebTalkStart","GLOBAL",1)
!AreaCheck("FH4800")
!AreaCheck("FHLMH1")
!AreaCheck("FHLMH2")
!AreaCheck("FHLUX1")
!AreaCheck("FHLUX2")
!AreaCheck("FHLUX3")
!AreaCheck("FHLUX4")
!AreaCheck("FHLUX5")
!AreaCheck("FHLUX6")
!AreaType(DUNGEON)
!Dead("FHLSEB")
InParty("FHLSEB")
CombatCounter(0)
OR(9)
Global("FHLAndTalk","GLOBAL",1)
Global("FHLAndTalk","GLOBAL",3)
Global("FHLAndTalk","GLOBAL",5)
Global("FHLAndTalk","GLOBAL",7)
Global("FHLAndTalk","GLOBAL",9)
Global("FHLAndTalk","GLOBAL",11)
Global("FHLAndTalk","GLOBAL",13)
Global("FHLAndTalk","GLOBAL",15)
Global("FHLAndTalk","GLOBAL",17)
THEN
RESPONSE #100
IncrementGlobal("FHLAndTalk","GLOBAL",1)
Interact(Player1)
END

IF
InParty(Myself)
Global("FHLLuxTalk","GLOBAL",0)
RealGlobalTimerExpired("FHLLuxTalkTime","GLOBAL")
!AreaCheck("FH4800")
!AreaCheck("FHLMH1")
!AreaCheck("FHLMH2")
!AreaCheck("FHLUX1")
!AreaCheck("FHLUX2")
!AreaCheck("FHLUX3")
!AreaCheck("FHLUX4")
!AreaCheck("FHLUX5")
!AreaCheck("FHLUX6")
!AreaType(DUNGEON)
!Dead("FHLSEB")
InParty("FHLSEB")
CombatCounter(0)
OR(10)
Global("FHLAndTalk","GLOBAL",2)
Global("FHLAndTalk","GLOBAL",4)
Global("FHLAndTalk","GLOBAL",6)
Global("FHLAndTalk","GLOBAL",8)
Global("FHLAndTalk","GLOBAL",10)
Global("FHLAndTalk","GLOBAL",12)
Global("FHLAndTalk","GLOBAL",14)
Global("FHLAndTalk","GLOBAL",16)
Global("FHLAndTalk","GLOBAL",18)
Global("FHLAndTalk","GLOBAL",20)
THEN
RESPONSE #100
RealSetGlobalTimer("FHLLuxTalkTime","GLOBAL",TWELVE_HOURS)
IncrementGlobal("FHLAndTalk","GLOBAL",1)
SetGlobal("FHLLuxTalk","GLOBAL",1)
END

//Initial Sebastian-Andrei talk timer//
IF
InParty(Myself)
RealGlobalTimerExpired("FHLAndDarkTime","GLOBAL")
Global("FHLAndSebFirstTalk","GLOBAL",0)
!Dead("FHLSEB")
InParty("FHLSEB")
!StateCheck("FHLSEB",CD_STATE_NOTVALID)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("FHLAndSebFirstTalk","GLOBAL",1)
StartDialogNoSet("FHLSEB")
END

IF
InParty(Myself)
RealGlobalTimerExpired("FHLLuxTalkTime","GLOBAL")
Global("FHLLuxTalk","GLOBAL",2)
Global("FHLAndSebTalkStart","GLOBAL",0)
!AreaCheck("FH4800")
!AreaCheck("FHLMH1")
!AreaCheck("FHLMH2")
!AreaCheck("FHLUX1")
!AreaCheck("FHLUX2")
!AreaCheck("FHLUX3")
!AreaCheck("FHLUX4")
!AreaCheck("FHLUX5")
!AreaCheck("FHLUX6")
!AreaType(DUNGEON)
!Dead("FHLSEB")
InParty("FHLSEB")
!StateCheck("FHLSEB",CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
OR(6)
Global("FHLAndSebTalk","GLOBAL",1)
Global("FHLAndSebTalk","GLOBAL",3)
Global("FHLAndSebTalk","GLOBAL",4)
Global("FHLAndSebTalk","GLOBAL",6)
Global("FHLAndSebTalk","GLOBAL",8)
Global("FHLAndSebTalk","GLOBAL",9)
THEN
RESPONSE #100
RealSetGlobalTimer("FHLLuxTalkTime","GLOBAL",TWELVE_HOURS)
SetGlobal("FHLAndSebTalkStart","GLOBAL",1)
SetGlobal("FHLLuxTalk","GLOBAL",0)
StartDialogueNoSet("FHLSEB")
END

IF
InParty(Myself)
InParty("FHLSEB")
!StateCheck("FHLSEB",CD_STATE_NOTVALID)
!AreaCheck("FH4800")
!AreaCheck("FHLMH1")
!AreaCheck("FHLMH2")
!AreaCheck("FHLUX1")
!AreaCheck("FHLUX2")
!AreaCheck("FHLUX3")
!AreaCheck("FHLUX4")
!AreaCheck("FHLUX5")
!AreaCheck("FHLUX6")
!AreaType(DUNGEON)
RealGlobalTimerExpired("FHLLuxTalkTime","GLOBAL")
GlobalGT("FHLLDP","GLOBAL",1)
Global("FHLAndSebTalk","GLOBAL",10)
Global("FHLAndSebTalkStart","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("FHLAndSebTalkStart","GLOBAL",1)
SetGlobal("FHLLuxTalk","GLOBAL",0)
StartDialogueNoSet("FHLSEB")
END

//If you're too flirty via player-initiated dialogue

IF
InParty(Myself)
GlobalGT("FHLAndFlirt","GLOBAL",5)
!Global("FHLAndSaysFriends","GLOBAL",2)
Global("FHLAndSaysFriendsTimer","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("FHLAndSaysFriendsTime","GLOBAL",350)
SetGlobal("FHLAndSaysFriendsTimer","GLOBAL",1)
END

IF
GlobalTimerExpired("FHLAndSaysFriendsTime","GLOBAL")
Global("FHLAndSaysFriendsTimer","GLOBAL",1)
CombatCounter(0)
GlobalGT("FHLAndFlirt","GLOBAL",5)
!Global("FHLAndSaysFriends","GLOBAL",2)
!Global("FHLAndSebTalkStart","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("FHLAndSaysFriends","GLOBAL",1)
StartDialogueNoSet(Player1)
END

//Lyel's appearances
IF
Global("FHLAndSebTalk","GLOBAL",6)
Global("FHLLuxTalk","GLOBAL",0)
Global("FHLJoined","GLOBAL",1)
Global("FHLLyelTimer","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("FHLLyelTimer","GLOBAL",1)
SetGlobalTimer("FHLLyelArrive","GLOBAL",ONE_DAY)
SetGlobal("FHLLuxTalk","GLOBAL",3)
END

IF
Global("FHLAndSebTalk","GLOBAL",7)
Global("FHLLuxTalk","GLOBAL",0)
Global("FHLJoined","GLOBAL",1)
Global("FHLLyelTimer","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("FHLLyelTimer","GLOBAL",2)
SetGlobalTimer("FHLLyelArrive2","GLOBAL",ONE_DAY)
SetGlobal("FHLLuxTalk","GLOBAL",3)
END

//Making Andrei move over to the estate

IF
Global("FHLReturn1Set","GLOBAL",1)
Global("FHLAndMoveEstate","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("FHLJoined","GLOBAL",0)
LeaveParty()
SetLeavePartyDialogFile()
ChangeAIScript("",DEFAULT)
ApplySpellRES("FHLDOOR",Myself)
Wait(1)
MoveBetweenAreas("FH4800",[2140.2409],4)
SetGlobal("FHLAndMoveEstate","GLOBAL",1)
END

//Making Andrei move over to the estate, #2//

IF
Global("FHLTempGone","GLOBAL",1)
Global("FHLAndMoveEstate2","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("FHLJoined","GLOBAL",0)
LeaveParty()
SetLeavePartyDialogFile()
ChangeAIScript("",DEFAULT)
ApplySpellRES("FHLDOOR",Myself)
Wait(1)
MoveBetweenAreas("FH4800",[2140.2409],4)
SetGlobal("FHLAndMoveEstate2","GLOBAL",1)
END

//Making Andrei leave permanently
IF
Global("FHLGone","GLOBAL",1)
Global("FHLAndLeave","GLOBAL",0)
InParty(Myself)
THEN
RESPONSE #100
SetLeavePartyDialogFile()
ChangeAIScript("",DEFAULT)
LeaveParty()
ApplySpellRES("FHLDOOR",Myself)
MoveBetweenAreas("FH4800",[2140.2409],4)
SetGlobal("FHLAndLeave","GLOBAL",1)
Wait(1)
DestroySelf()
END

//Delaying the banters while in the estate or they're temporarily gone

IF
Global("FHLLuxTalk","GLOBAL",3)
InParty("FHLAND")
InParty("FHLSEB")
OR(2)
Global("FHLXavTalk","GLOBAL",1)
Global("FHLNoXavTalk","GLOBAL",1)
Global("FHLLuxResume","GLOBAL",0)
!AreaCheck("FH4800")
!AreaCheck("FHLMH1")
!AreaCheck("FHLMH2")
!AreaCheck("FHLUX1")
!AreaCheck("FHLUX2")
!AreaCheck("FHLUX3")
!AreaCheck("FHLUX4")
!AreaCheck("FHLUX5")
!AreaCheck("FHLUX6")
THEN
RESPONSE #100
SetGlobal("FHLLuxTalk","GLOBAL",0)
SetGlobal("FHLLuxResume","GLOBAL",1)
RealSetGlobalTimer("FHLLuxTalkTime","GLOBAL",900)
END

IF
Global("FHLLuxTalk","GLOBAL",3)
Global("FHLSebPostLyel","GLOBAL",2)
Global("FHLLuxResume2","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("FHLLuxResume2","GLOBAL",1)
SetGlobal("FHLLuxTalk","GLOBAL",0)
RealSetGlobalTimer("FHLLuxTalkTime","GLOBAL",900)
END

IF
!Dead("FHLAND")
!Dead("FHLSEB")
InParty("FHLAND")
InParty("FHLSEB")
GlobalTimerExpired("FHLLyelArrive2","GLOBAL")
Global("FHLJoined","GLOBAL",1)
Global("FHLSpawnLyel2","GLOBAL",0)
AreaType(OUTDOOR)
THEN
RESPONSE #100
CreateCreatureObjectDoor("FHLLYEL2",Myself,0,0,0)
ActionOverride("FHLLYEL2",MoveToObject("FHLAND"))
SetGlobal("FHLSpawnLyel2","GLOBAL",1)
Wait(5)
ActionOverride("FHLLYEL2",StartDialogueNoSet(Player1))
END

//Apology talk
IF
InParty("FHLAND")
GlobalTimerExpired("FHLAndApologyTime","GLOBAL")
Global("FHLAndApology","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("FHLAndApology","GLOBAL",1)
StartDialogNoSet(Player1)
END


//Show that he's in a row with Sebastian
IF
Global("FHLRow","GLOBAL",0)
Global("FHLSebPostLyel","GLOBAL",2)
GlobalLT("FHLAndSebTalk","GLOBAL",10)
THEN
RESPONSE #100
SetGlobal("FHLRow","GLOBAL",1)
END

IF
Global("FHLRow","GLOBAL",1)
GlobalGT("FHLAndSebTalk","GLOBAL",10)
THEN
RESPONSE #100
SetGlobal("FHLRow","GLOBAL",0)
END

//MDP
IF
Global("FHLMDP","GLOBAL",1)
InParty(Myself)
THEN
RESPONSE #100
LeaveParty()
END

IF
Global("FHLMDP","GLOBAL",1)
!Allegiance(Myself,ENEMY)
THEN
RESPONSE #100
Enemy()
END

IF
Allegiance(Myself,ENEMY)
HPPercentLT(Myself,50)
!RealGlobalTimerNotExpired("DrankPotionTimer","LOCALS")
HasItem("POTN52",Myself)
THEN
RESPONSE #100
RealSetGlobalTimer("DrankPotionTimer","LOCALS",10)
DisplayStringHead(Myself,46150)
UseItem("POTN52",Myself)
END

IF
Allegiance(Myself,ENEMY)
See(NearestEnemyOf(Myself))
OR(2)
Range(NearestMyGroupOfType,20)
See(NearestMyGroupOfType([0.0.0.CLERIC_ALL]))
HPPercentLT(Myself,35)
!RealGlobalTimerNotExpired("CalledForHealing","LOCALS")
THEN
RESPONSE #100
RealSetGlobalTimer("CalledForHealing","LOCALS",10)
Shout(HEALME)
Continue()
END

IF
Allegiance(Myself,ENEMY)
Heard(NearestMyGroupOfType,HEALME)
HPPercentGT(Myself,75)
See(LastHeardBy(Myself))
Range(LastHeardBy(Myself),10)
!Range(NearestMyGroupOfType([0.0.0.CLERIC_ALL]),30)
!NumItemsGT("POTN55",LastHeardBy(Myself),0)
NumItemsGT("POTN55",Myself,1)
!RealGlobalTimerNotExpired("GaveAwayPotion","LOCALS")
THEN
RESPONSE #100
RealSetGlobalTimer("GaveAwayPotion","LOCALS",10)
GiveItem("POTN55",LastHeardBy(Myself))
Continue()
END

IF
Allegiance(Myself,ENEMY)
HPPercentLT(Myself,60)
Global("InvulnerabilityPotion","LOCALS",0)
!RealGlobalTimerNotExpired("DrankPotionTimer","LOCALS")
HasItem("POTN11",Myself)
THEN
RESPONSE #100
RealSetGlobalTimer("DrankPotionTimer","LOCALS",10)
SetGlobal("InvulnerabilityPotion","LOCALS",1)
DisplayStringHead(Myself,46150)
UseItem("POTN11",Myself)
END

IF
Allegiance(Myself,ENEMY)
HPPercentLT(Myself,60)
HaveSpell("4815")
!RealGlobalTimerNotExpired("LayOnHands","LOCALS")
THEN
RESPONSE #100
RealSetGlobalTimer("LayOnHands","LOCALS",10)
Spell(Myself,"4815")
END

IF
Allegiance(Myself,ENEMY)
!RealGlobalTimerNotExpired("MonkTimer","LOCALS")
See(NearestEnemyOf(Myself))
HaveSpell("4820")
RandomNum(2,1)
THEN
RESPONSE #100
SetGlobalTimer("MonkTimer","LOCALS",SIX_MINUTES)
Spell(Myself,"4820")
AttackReevaluate(NearestEnemyOf(Myself),45)
END

IF
Allegiance(Myself,ENEMY)
!GlobalTimerNotExpired("MonkTimer","LOCALS")
See(NearestEnemyOf(Myself))
HaveSpell("4811")
RandomNum(2,1)
THEN
RESPONSE #100
SetGlobalTimer("MonkTimer","LOCALS",SIX_MINUTES)
Spell(Myself,"4811")
AttackReevaluate(NearestEnemyOf(Myself),45)
END

IF
Allegiance(Myself,ENEMY)
!HPPercentLT(Myself,20)
THEN
RESPONSE #100
EquipMostDamagingMelee()
AttackReevaluate(NearestEnemyOf(Myself),45)
END

IF
Allegiance(Myself,ENEMY)
HPPercentLT(Myself,20)
OR(3)
HasItem("POTN55",Myself)
HaveSpell("4815")
Range(NearestMyGroupOfType,20)
THEN
RESPONSE #100
RunAwayFrom(NearestEnemyOf(Myself),45)
END

IF
Allegiance(Myself,ENEMY)
HPPercentLT(Myself,20)
!HasItem("POTN52",Myself)
!HaveSpell("4815")
!Range(NearestMyGroupOfType,20)
THEN
RESPONSE #100
EquipMostDamagingMelee()
AttackReevaluate(NearestEnemyOf(Myself),45)
END

//String fixer
IF
HotKey(K)
THEN
RESPONSE #100
StartCutSceneMode()
StartCutScene("FHLAFIX")
END
