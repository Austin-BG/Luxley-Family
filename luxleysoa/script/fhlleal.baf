IF
AttackedBy([ANYONE],DEFAULT)
!Allegiance(Myself,ENEMY)
THEN
RESPONSE #100
Enemy()
END

IF
HPPercentLT(Myself,20)
Global("FHLSnakeChange","LOCALS",0)
!Dead("FHLXAV")
THEN
RESPONSE #100
SetGlobal("FHLSnakeChange","LOCALS",1)
SetGlobalTimer("FHLLealChange","GLOBAL",90)
ApplySpellRES("FHLAND",MYSELF)
END

IF
Global("FHLSnakeChange","LOCALS",1)
GlobalTimerExpired("FHLLealChange","GLOBAL")
THEN
RESPONSE #100
SetGlobal("FHLSnakeChange","LOCALS",0)
ApplySpellRES("FHL491",MYSELF)
END

IF
Global("FHLSnakeChange","LOCALS",1)
Dead("FHLXAV")
THEN
RESPONSE #100
SetGlobal("FHLSnakeChange","LOCALS",0)
ApplySpellRES("FHL491",MYSELF)
END


IF
See(NearestEnemyOf(Myself))
Global("prep","LOCALS",0)
THEN
RESPONSE #100
ReallyForceSpell(Myself,CLERIC_DRAW_UPON_HOLY_MIGHT)
ReallyForceSpell(Myself,CLERIC_DEFENSIVE_HARMONY)
SetGlobal("prep","LOCALS",1)
END

IF
OR(3)
StateCheck("FHLLUC",STATE_PANIC)
StateCheck("FHLSIM",STATE_PANIC)
StateCheck("FHLROS",STATE_PANIC)
HaveSpell(CLERIC_REMOVE_FEAR)
THEN
RESPONSE #100
Spell(Myself,CLERIC_REMOVE_FEAR)
END

IF
HPPercentLT("FHLLUC",50)
HaveSpell(CLERIC_CURE_SERIOUS_WOUNDS)
THEN
RESPONSE #100
Spell("FHLLUC",CLERIC_CURE_SERIOUS_WOUNDS)
END

IF
HPPercentLT("FHLROS",50)
HaveSpell(CLERIC_CURE_SERIOUS_WOUNDS)
THEN
RESPONSE #100
Spell("FHLROS",CLERIC_CURE_SERIOUS_WOUNDS)
END

IF
HPPercentLT("FHLSIM",50)
HaveSpell(CLERIC_CURE_SERIOUS_WOUNDS)
THEN
RESPONSE #100
Spell("FHLSIM",CLERIC_CURE_SERIOUS_WOUNDS)
END

IF
See(NearestEnemyOf(Myself))
Alignment(NearestEnemyOf(Myself),MASK_EVIL)
HaveSpell(CLERIC_HOLY_SMITE)
THEN
RESPONSE #100
Spell(NearestEnemyOf(Myself),CLERIC_HOLY_SMITE)
END

IF
See(NearestEnemyOf(Myself))
HaveSpell(CLERIC_FLAME_STRIKE)
THEN
RESPONSE #100
Spell(NearestEnemyOf(Myself),CLERIC_FLAME_STRIKE)
END

IF
See(NearestEnemyOf(Myself))
HaveSpell(CLERIC_CONJURE_ANIMALS)
THEN
RESPONSE #100
Spell(NearestEnemyOf(Myself),CLERIC_CONJURE_ANIMALS)
END

IF
See(NearestEnemyOf(Myself))
HaveSpell(CLERIC_BOLT_OF_GLORY)
THEN
RESPONSE #100
Spell(NearestEnemyOf(Myself),CLERIC_BOLT_OF_GLORY)
END

IF
See(NearestEnemyOf(Myself))
HaveSpell(CLERIC_HOLD_PERSON)
!StateCheck(NearestEnemyOf(Myself),STATE_SLEEPING)
!StateCheck(NearestEnemyOf(Myself),STATE_HELPLESS)
THEN
RESPONSE #100
Spell(NearestEnemyOf(Myself),CLERIC_HOLD_PERSON)
END

IF
See(NearestEnemyOf(Myself))
HaveSpell(CLERIC_POISON)
!StateCheck(NearestEnemyOf(Myself),STATE_POISONED)
THEN
RESPONSE #100
Spell(NearestEnemyOf(Myself),CLERIC_POISON)
END

IF
See(NearestEnemyOf(Myself))
THEN
RESPONSE #100
EquipMostDamagingMelee()
AttackReevaluate(NearestEnemyOf(Myself),15)
END

IF
Global("FHLMDP","GLOBAL",1)
Global("FHLLealMove","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("FHLLealMove","GLOBAL",1)
MoveBetweenAreas("FHLMH1",[1343.956],9)
END
