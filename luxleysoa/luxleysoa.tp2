BACKUP ~luxleysoa/backup~

AUTHOR ~feuille_sakaki@spellholdstudios.net~

VERSION ~v1.2~

README ~luxleysoa/LuxleyReadme.htm~

ALWAYS

	ACTION_IF NOT VARIABLE_IS_SET always_once_section BEGIN // Always block must run only once per install!

		OUTER_SET always_once_section = 1

		/* ----------------------------------------- *
		 *    Determines which game is installed.    *
		 * ----------------------------------------- */

		OUTER_SET is_tob   = (GAME_IS ~bgt tob tutu tutu_totsc~) ? 1 : 0     // based on vanilla ToB engine
		OUTER_SET is_bg2ee = (GAME_IS "bg2ee") ? 1 : 0                       // b2gee
		OUTER_SET is_eet   = (GAME_IS "eet") ? 1 : 0                         // eet
		OUTER_SET is_ee    = (GAME_IS ~bg2ee eet~) ? 1 : 0                   // EE game
		OUTER_SET is_tobex = (FILE_EXISTS ~tobex_ini/tobexcore.ini~) ? 1 : 0 // ToBEx is installed


		/* ------------------------------------------------------- *
		 *    Sets default variables if they're not set already    *
		 * ------------------------------------------------------- */

		// Variable used to properly install specific EE games ressources
		ACTION_IF (is_ee) BEGIN
			OUTER_SPRINT ee_file "_ee"
		END ELSE BEGIN
			OUTER_SPRINT ee_file ""
		END

		// Variable used to properly install Sebastian kit
		ACTION_IF (is_ee OR is_tobex) BEGIN
			OUTER_SET kits_lim_256  = 0
		END ELSE BEGIN
			OUTER_SET kits_lim_256  = 1
		END


	END

END


// Languages
LANGUAGE ~English~
         ~english~
         ~luxleysoa/lang/english/setup.tra~


/* ====================================================== *
 *    Luxley Family Mod for BGII:SoA  (main component)    *
 * ====================================================== */

BEGIN @1 // ~Luxley Family Mod for BGII:SoA~

INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~



/* ===================================================================================================================================== *
 *    Portraits for non-joinable NPCs  (optional component)  :  install it if you want custom portraits for various non-joinable NPCs    *
 * ===================================================================================================================================== */

BEGIN @300 // ~Portraits for non-joinable NPCs~

COPY ~luxleysoa/portraits~ ~override~



/* ========================================================================================================================== *
 *    Nathaniel Content  (optional component)  :  these will be deprecated by a future release of the Crossmod Banter Pack    *
 * ========================================================================================================================== */

BEGIN @400                             // ~Nathaniel Content~
REQUIRE_FILE ~override/fhnat.dlg~ @401 // ~You do not appear to have Nathaniel installed.~

COMPILE ~luxleysoa/crossmod/fhlnath.d~
EXTEND_BOTTOM ~fhnat.bcs~ ~luxleysoa/crossmod/fhlnath.baf~
