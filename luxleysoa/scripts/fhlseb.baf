//CONTENTS//

//Greetings
//Transformation script
//Parting script
//If Andrei dies
//Talk timers
//Quest script
//Biography change
//String fixer
//Mod romance auto-kills

//Initial meeting
IF
Global("FHLSebCat","GLOBAL",0)
Global("FHLMeetLux","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("FHLSebCat","GLOBAL",1)
ApplySpellRES("FHLSEB",MYSELF)
END

IF
Global("FHLMeetLux","GLOBAL",1)
!Dead("FHLCRIM1")
THEN
RESPONSE #100
Attack("FHLCRIM1")
END

IF
Global("FHLLuxMove","GLOBAL",1)
Global("FHLSebMove","GLOBAL",0)
THEN
RESPONSE #100
EscapeAreaMove("AR0700",2140,2409,0)
SetGlobal("FHLSebMove","GLOBAL",1)
END

//Cat transformation
IF
!Dead("FHLXAV")
HPPercentLT(Myself,20)
Global("FHLCatChange","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("FHLCatChange","LOCALS",1)
SetGlobalTimer("FHLSebChange","GLOBAL",90)
ApplySpellRES("FHLSEB",MYSELF)
END

IF
Global("FHLCatChange","LOCALS",1)
GlobalTimerExpired("FHLSebChange","GLOBAL")
THEN
RESPONSE #100
SetGlobal("FHLCatChange","LOCALS",0)
ApplySpellRES("FHL491",MYSELF)
END

IF
Global("FHLCatChange","LOCALS",1)
Dead("FHLXAV")
THEN
RESPONSE #100
SetGlobal("FHLCatChange","LOCALS",0)
ApplySpellRES("FHL491",MYSELF)
END

//Ensure Sebastian leaves the party when Andrei does

IF
InParty("FHLSEB")
!Dead("FHLAND")
!InParty("FHLAND")
Global("FHLReturns","GLOBAL",0)
Global("FHLKicked","GLOBAL",1)
CombatCounter(0)
THEN
RESPONSE #100
LeaveParty()
SetLeavePartyDialogFile()
ChangeAIScript("",DEFAULT)
EscapeAreaMove("AR0700",2140,2409,4)
END

IF
InParty("FHLSEB")
!Dead("FHLAND")
!InParty("FHLAND")
Global("FHLReturns","GLOBAL",0)
Global("FHLKicked","GLOBAL",2)
CombatCounter(0)
THEN
RESPONSE #100
LeaveParty()
SetLeavePartyDialogFile()
ChangeAIScript("",DEFAULT)
END

IF
Global("FHLReturns","GLOBAL",0)
Global("FHLKicked","GLOBAL",3)
InParty(Myself)
THEN
RESPONSE #100
LeaveParty()
EscapeArea()
END

//If Andrei dies
IF
InParty("FHLSEB")
Dead("FHLAND")
!InPartyAllowDead("FHLAND")
Global("FHLJoined","GLOBAL",1)
Global("FHLMDP","GLOBAL",0)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("FHLAndDead","GLOBAL",1)
StartDialogueNoSet(Player1)
END

//Sebastian-PC talks
IF
InParty(Myself)
Global("FHLLuxTalk","GLOBAL",1)
!Global("FHLAndSebTalkStart","GLOBAL",1)
RealGlobalTimerExpired("FHLLuxTalkTime","GLOBAL")
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
!AreaCheck("FH4800")
!AreaCheck("FHLMH1")
!AreaCheck("FHLMH2")
!AreaCheck("FHLUX1")
!AreaCheck("FHLUX2")
!AreaCheck("FHLUX3")
!AreaCheck("FHLUX4")
!AreaCheck("FHLUX5")
!AreaCheck("FHLUX6")
!AreaType(DUNGEON)
!Dead("FHLSEB")
CombatCounter(0)
OR(9)
Global("FHLSebTalk","GLOBAL",1)
Global("FHLSebTalk","GLOBAL",3)
Global("FHLSebTalk","GLOBAL",5)
Global("FHLSebTalk","GLOBAL",7)
Global("FHLSebTalk","GLOBAL",9)
Global("FHLSebTalk","GLOBAL",11)
Global("FHLSebTalk","GLOBAL",13)
Global("FHLSebTalk","GLOBAL",15)
Global("FHLSebTalk","GLOBAL",17)
THEN
RESPONSE #100
IncrementGlobal("FHLSebTalk","GLOBAL",1)
Interact(Player1)
END

IF
InParty(Myself)
Global("FHLLuxTalk","GLOBAL",1)
RealGlobalTimerExpired("FHLLuxTalkTime","GLOBAL")
!AreaCheck("FH4800")
!AreaCheck("FHLMH1")
!AreaCheck("FHLMH2")
!AreaCheck("FHLUX1")
!AreaCheck("FHLUX2")
!AreaCheck("FHLUX3")
!AreaCheck("FHLUX4")
!AreaCheck("FHLUX5")
!AreaCheck("FHLUX6")
!AreaType(DUNGEON)
CombatCounter(0)
OR(10)
Global("FHLSebTalk","GLOBAL",2)
Global("FHLSebTalk","GLOBAL",4)
Global("FHLSebTalk","GLOBAL",6)
Global("FHLSebTalk","GLOBAL",8)
Global("FHLSebTalk","GLOBAL",10)
Global("FHLSebTalk","GLOBAL",12)
Global("FHLSebTalk","GLOBAL",14)
Global("FHLSebTalk","GLOBAL",16)
Global("FHLSebTalk","GLOBAL",18)
Global("FHLSebTalk","GLOBAL",20)
THEN
RESPONSE #100
RealSetGlobalTimer("FHLLuxTalkTime","GLOBAL",TWELVE_HOURS)
IncrementGlobal("FHLSebTalk","GLOBAL",1)
SetGlobal("FHLLuxTalk","GLOBAL",2)
END

//Morning after talk

IF
InParty(Myself)
PartyRested()
Global("FHLDoneTheNasty","GLOBAL",1)
See(Player1)
CombatCounter(0)
Global("FHLFirstMorningAfter","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("FHLFirstMorningAfter","GLOBAL",2)
StartDialogueNoSet(Player1)
END

//Sebastian-Andrei talks
IF
InParty(Myself)
Global("FHLLuxTalk","GLOBAL",2)
Global("FHLAndSebTalkStart","GLOBAL",0)
RealGlobalTimerExpired("FHLLuxTalkTime","GLOBAL")
!AreaCheck("FH4800")
!AreaCheck("FHLMH1")
!AreaCheck("FHLMH2")
!AreaCheck("FHLUX1")
!AreaCheck("FHLUX2")
!AreaCheck("FHLUX3")
!AreaCheck("FHLUX4")
!AreaCheck("FHLUX5")
!AreaCheck("FHLUX6")
!AreaType(DUNGEON)
!Dead("FHLAND")
InParty("FHLAND")
!StateCheck("FHLAND",CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
OR(3)
Global("FHLAndSebTalk","GLOBAL",2)
Global("FHLAndSebTalk","GLOBAL",5)
Global("FHLAndSebTalk","GLOBAL",7)
THEN
RESPONSE #100
SetGlobal("FHLLuxTalk","GLOBAL",0)
SetGlobal("FHLAndSebTalkStart","GLOBAL",1)
RealSetGlobalTimer("FHLLuxTalkTime","GLOBAL",TWELVE_HOURS)
StartDialogueNoSet("FHLAND")
END

//Moving to go to the estate
IF
Global("FHLReturn1Set","GLOBAL",1)
Global("FHLSebMoveEstate","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("FHLJoined","GLOBAL",0)
LeaveParty()
SetLeavePartyDialogFile()
ChangeAIScript("",DEFAULT)
ApplySpellRES("FHLDOOR",Myself)
Wait(1)
MoveBetweenAreas("FH4800",[2140.2409],4)
SetGlobal("FHLSebMoveEstate","GLOBAL",1)
END

//Moving to the estate, #2
IF
Global("FHLTempGone","GLOBAL",1)
Global("FHLSebMoveEstate2","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("FHLJoined","GLOBAL",0)
LeaveParty()
SetLeavePartyDialogFile()
ChangeAIScript("",DEFAULT)
ApplySpellRES("FHLDOOR",Myself)
Wait(1)
MoveBetweenAreas("FH4800",[2140.2409],4)
SetGlobal("FHLSebMoveEstate2","GLOBAL",1)
END

//Returning from the estate
IF
Global("FHLReturns","GLOBAL",1)
Range([PC],8)
See("FHLAND")
THEN
RESPONSE #100
StartDialogNoSet(Player1)
END

//Returning from the estate, #2
IF
Global("FHLReturns2","GLOBAL",1)
Range([PC],8)
See("FHLAND")
THEN
RESPONSE #100
StartDialogNoSet(Player1)
END

//Making Sebastian leave permanently
IF
Global("FHLGone","GLOBAL",1)
Global("FHLSebLeave","GLOBAL",0)
InParty(Myself)
THEN
RESPONSE #100
SetLeavePartyDialogFile()
ChangeAIScript("",DEFAULT)
LeaveParty()
ApplySpellRES("FHLDOOR",Myself)
MoveBetweenAreas("FH4800",[2140.2409],4)
SetGlobal("FHLSebLeave","GLOBAL",1)
SetGlobal("FHLJoined","GLOBAL",0)
Wait(1)
DestroySelf()
END

//Quest dialogue scripting
IF
Global("FHLSebPostLyel","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

//Curse-curing time
IF
GlobalTimerExpired("FHLCureCurse","GLOBAL")
Global("FHLAndSebTalk","GLOBAL",10)
Global("FHLAndTalk","GLOBAL",19)
Global("FHLSebTalk","GLOBAL",19)
Global("FHLTellCurse","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("FHLTellCurse","GLOBAL",1)
END

IF
Global("FHLTellCurse","GLOBAL",2)
THEN
RESPONSE #100
Wait(3)
StartDialogueNoSet(Player1)
END

IF
Global("FHLLDP","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

IF
Global("FHLMDP","GLOBAL",1)
!AreaCheck("FH4800")
!AreaCheck("FHLMH1")
!AreaCheck("FHLMH2")
!AreaCheck("FHLUX1")
!AreaCheck("FHLUX2")
!AreaCheck("FHLUX3")
!AreaCheck("FHLUX4")
!AreaCheck("FHLUX5")
!AreaCheck("FHLUX6")
CombatCounter(0)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

//Biography change

IF
GlobalGT("FHLMDP","GLOBAL",0)
Global("FHLSebBioChange","GLOBAL",0)
THEN
RESPONSE #100
SetPlayerSound(Myself,@128,EXISTANCE5)
SetGlobal("FHLSebBioChange","GLOBAL",1)
END

//String fixer
IF
HotKey(K)
THEN
RESPONSE #100
StartCutSceneMode()
StartCutScene("FHLSFIX")
END

//Mod romance auto-kills

IF
InParty("FHLSEB")
Global("FHLBrushOff","GLOBAL",0)
GlobalGT("FHLMDP","GLOBAL",1)
OR(14)
Global("ChloeRomanceActiveCR","GLOBAL",2)
Global("E3FadeRomanceActive","GLOBAL",2)
Global("B!GavRA","GLOBAL",2)
Global("imoenromanceactive","GLOBAL",2)
Global("J#KelseyRomanceActive","GLOBAL",2)
Global("KiaraRomanceActive","GLOBAL",2)
Global("P#KivanRomanceActive","GLOBAL",2)
Global("SaerilethRomanceActive","GLOBAL",2)
Global("SarahRomanceActive","GLOBAL",2)
Global("SimeRomanceActive","GLOBAL",2)
GlobalGT("SolaTalk","GLOBAL",13)
Global("TsujathaRomanceActive","GLOBAL",2)
Global("TashiaRomanceActive","GLOBAL",2)
Global("O#XanRomanceActive","GLOBAL",2)
THEN
RESPONSE #100
SetGlobal("FHLBrushOff","GLOBAL",1)
END
