/* ============================================== *
 *         MAIN   COMPONENT  INSTALLATION         *
 * ============================================== */

SILENT

/* -------------------------------------------------------------------------------------------------- *
 *     Adds CamDawg's custom IsValidForPartyDialogue state used throughout the mod. Thanks Cam!       *
 * -------------------------------------------------------------------------------------------------- *
 *  covers STATE_CONFUSED, STATE_FEEBLEMINDED, STATE_SILENCED, STATE_DEAD, STATE_ACID_DEATH,          *
 *  STATE_FLAME_DEATH, STATE_EXPLODING_DEATH, STATE_STONE_DEATH, STATE_FROZEN_DEATH, STATE_HELPLESS,  *
 *  STATE_STUNNED, STATE_PANIC, STATE_BERSERK, STATE_SLEEPING                                         *
 * -------------------------------------------------------------------------------------------------- */
APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~


/* -------------------------------- *
 *    Loads macros and functions    *
 * -------------------------------- */

INCLUDE ~%MOD_FOLDER%/lib/gw_functions.tpa~ // Loads macros and functions library dealing with tooltip appending, specific EE items descriptions, and more...


/* ---------------------------- *
 *  Fixing Imoen's banter file  *
 * ---------------------------- */

// Add Imoen's banter file if Fixpack isn't installed
// --------------------------------------------------
ACTION_IF NOT FILE_EXISTS_IN_GAME ~cdbehbla.pro~ BEGIN
	ACTION_IF (is_tob OR is_ee) BEGIN 
		COPY_EXISTING ~interdia.2da~ ~override~
			SET_2DA_ENTRY 17 1 2 ~BIMOEN2~ // fixes ToB version
		BUT_ONLY
	END ELSE BEGIN
		APPEND ~interdia.2da~ ~IMOEN       BIMOEN2~ UNLESS ~[ %TAB%]BIMOEN2[ %TAB%%LNL%%MNL%%WNL%]~ // fixes SoA version
	END
END

// Gives Imoen a banter file if there isn't one already
// ----------------------------------------------------
ACTION_IF NOT FILE_EXISTS_IN_GAME ~bimoen2.dlg~ BEGIN
	<<<<<<<< luxleysoa/dialogues/bimoen2.d
  BEGIN BIMOEN2
>>>>>>>>
	COMPILE ~luxleysoa/dialogues/bimoen2.d~
END


/* ------------------------------------------ *
 *  Fixing .ids, courtesy of the BG2 Fixpack  *
 * ------------------------------------------ */

// Adds ToB trigger/actions to SoA
// -------------------------------
INCLUDE ~%MOD_FOLDER%/lib/tob2soa.tph~

// Adds timers entry in gtimes.ids
// -------------------------------
INCLUDE ~%MOD_FOLDER%/lib/ids_entries_gtimes.tph~

// Adds kit.ids fixer for classical games
// --------------------------------------
ACTION_IF NOT (is_ee) BEGIN
	INCLUDE ~%MOD_FOLDER%/lib/kit_ids_fixer.tpa~
END


/* -------------------------------------- *
 *  Installing spells and graphics files  *
 * -------------------------------------- */

PRINT @11 // ~Installing spells and graphics...~
SILENT

COPY ~%MOD_FOLDER%/spells/copy~ ~override~

// Dark Rage
COPY ~%MOD_FOLDER%/spells/fhlrage.spl~ ~override~
	SAY NAME1 @12
	SAY NAME2 @12

// Installing NPC portraits
// ------------------------
COPY ~%MOD_FOLDER%/portraits%ee_file%~ ~override~

// Copy BAMs or convert them to EE
// -------------------------------
ACTION_IF is_ee BEGIN
	INCLUDE ~%MOD_FOLDER%/lib/gw_convert_inventory_bam_to_ee.tpa~ // Loads function converting inventory BAMs to EE.
	OUTER_SPRINT file-exc ~tigid ~                                // Special case: description bams don't need to be converted.
	ACTION_BASH_FOR ~%MOD_FOLDER%/bam/~ ~^.+\.bam$~ BEGIN
		ACTION_TO_LOWER %BASH_FOR_FILE%
		ACTION_TO_LOWER %BASH_FOR_RES%
		ACTION_IF (~%file-exc%~ STRING_CONTAINS_REGEXP ~%BASH_FOR_RES%~ = 1) BEGIN
			COPY ~%BASH_FOR_FILESPEC%~ ~override/%BASH_FOR_FILE%~
				LPF ~gw_convert_inventory_bam_to_ee~ END
			BUT_ONLY
		END ELSE BEGIN
			COPY ~%BASH_FOR_FILESPEC%~ ~override/%BASH_FOR_FILE%~
		END
	END
END ELSE BEGIN
	COPY ~%MOD_FOLDER%/bam~	~override~
END


/* ------------------------ *
 *  Installing audio files  *
 * ------------------------ */

PRINT @21 // ~Installing audio files...~
SILENT

// Installing  music files
// -----------------------
COPY ~%MOD_FOLDER%/song/fhlblank.mus~ ~music~
     ~%MOD_FOLDER%/song/fhlmu1.mus~   ~music~
     ~%MOD_FOLDER%/song/fhlmu2.mus~   ~music~
MKDIR ~music/fhlmu~
COPY ~%MOD_FOLDER%/song/fhlmu1.acm~ ~music/fhlmu~
     ~%MOD_FOLDER%/song/fhlmu1.acm~ ~music/fhlmu~

COPY_EXISTING ~songlist.2da~ ~override~
	SET_2DA_ENTRY 0 2 3 ~fhlblank.mus~

ADD_MUSIC  ~Luxley1~    ~music/fhlmu1.mus~
ADD_MUSIC  ~Luxley2~    ~music/fhlmu2.mus~

// Installing  audio files
// -----------------------
LAF HANDLE_AUDIO END


/* --------------------- *
 *  Installing new area  *
 * --------------------- */

PRINT @31 // ~Installing area files...~
SILENT

COPY ~%MOD_FOLDER%/areas/copy~ ~override~

// Adding notes
// ------------
COPY ~%MOD_FOLDER%/areas/fh4800.are~ ~override~
	ADD_MAP_NOTE #3041 #330  ~gray~ @32 // ~The Main House~
	ADD_MAP_NOTE #3001 #2555 ~gray~ @33 // ~Training Ground~
	ADD_MAP_NOTE #1579 #2416 ~gray~ @34 // ~Lealia's Home~
	ADD_MAP_NOTE #365  #2667 ~gray~ @35 // ~Way Out~


// Installing tis files according to the game
// ------------------------------------------
ACTION_IF is_ee BEGIN
	INCLUDE ~%MOD_FOLDER%/lib/functions.tph~ // For new locations in EE: checks if there are already pvrz files with the same name so that there are no conflicts with other mods
	ACTION_FOR_EACH tis_name_all IN fh4800 fhlmh1 fhlmh2 fhlux1 fhlux2 fhlux3 fhlux4 fhlux5 fhlux6 BEGIN
		LAF ~FC_COPY_TIS_PVRZ~ // Finds free pvrz indices
			STR_VAR
				tis_pvrz_path = EVAL "%MOD_FOLDER%/tis/ee"
				path_to_vanilla_tis = EVAL "%MOD_FOLDER%\tis\vanilla"
				tis_name = EVAL "%tis_name_all%"
		END
	END
END ELSE BEGIN
	COPY ~%MOD_FOLDER%\tis\vanilla~ ~override~
END

// Adding the main area to the master area file
// --------------------------------------------
APPEND ~mastarea.2da~ ~FH4800 value~


/* --------------------------------- *
 *  Compiling scripts and dialogues  *
 * --------------------------------- */

PRINT @41 // ~Compiling dialogues and scripts...~

COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/dialogues~
COMPILE ~%MOD_FOLDER%/scripts~

EXTEND_BOTTOM ~ar0319.bcs~  ~%MOD_FOLDER%/scriptsext/fhl0319.baf~
EXTEND_BOTTOM ~ar0400.bcs~  ~%MOD_FOLDER%/scriptsext/fhl0400.baf~
EXTEND_BOTTOM ~ar0500.bcs~  ~%MOD_FOLDER%/scriptsext/fhl0500.baf~
EXTEND_BOTTOM ~ar0513.bcs~  ~%MOD_FOLDER%/scriptsext/fhl0513.baf~
EXTEND_BOTTOM ~ar0700.bcs~  ~%MOD_FOLDER%/scriptsext/fhl0700.baf~
EXTEND_BOTTOM ~ar0810.bcs~  ~%MOD_FOLDER%/scriptsext/fhl0810.baf~
EXTEND_BOTTOM ~baldur.bcs~  ~%MOD_FOLDER%/scriptsext/fhlbaldur.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~aerie.bcs~   ~%MOD_FOLDER%/scriptsext/fhlaer.baf~
EXTEND_BOTTOM ~anomen.bcs~  ~%MOD_FOLDER%/scriptsext/fhlano.baf~
EXTEND_BOTTOM ~jaheira.bcs~ ~%MOD_FOLDER%/scriptsext/fhljah.baf~
EXTEND_BOTTOM ~viconia.bcs~ ~%MOD_FOLDER%/scriptsext/fhlvic.baf~


/* ------------------ *
 *  Installing items  *
 * ------------------ */

PRINT @51 // ~Installing custom items...~
SILENT

// Sebastian's Book
COPY ~%MOD_FOLDER%/items/fhlbook.itm~ ~override~
	SAY NAME1 @52
	SAY NAME2 @52
	SAY DESC @53

COPY ~%MOD_FOLDER%/items/fhlbk2.itm~ ~override~
	SAY NAME1 @52
	SAY NAME2 @52
	SAY DESC @54

// Mietta
COPY ~%MOD_FOLDER%/items/fhltig.itm~ ~override~
	SAY NAME1 @55
	SAY NAME2 @55
	SAY DESC @56

// Note
COPY ~%MOD_FOLDER%/items/fhlnote.itm~ ~override~
	SAY NAME1 @57
	SAY NAME2 @57
	SAY DESC @58

// Old sword
COPY ~%MOD_FOLDER%/items/fhlsw.itm~ ~override~
	SAY NAME1 @59
	SAY NAME2 @59
	LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@60" END

// Aegelfred's Chronicles
COPY ~%MOD_FOLDER%/items/fhlchr.itm~ ~override~
	SAY NAME1 @61
	SAY NAME2 @61
	SAY DESC @62


/* ----------------------------- *
 *  Adding Bard Kit: Playwright  *
 * ----------------------------- */

// Only if the kit isn't already installed (don't ask me why!!!)
ACTION_IF NOT FILE_EXISTS_IN_GAME ~fhlplay.fh~ BEGIN
	INCLUDE ~%MOD_FOLDER%/lib/fhlplay.tph~
END


/* ---------------------- *
 *  Installing creatures  *
 * ---------------------- */

PRINT @81 // ~Installing creature files...~
SILENT

// Sebastian
// ---------
COPY ~%MOD_FOLDER%/cre/fhlseb08.cre~ ~override~
	SAY NAME1 @82
	SAY NAME2 @82
	WRITE_ASCII 0x34  ~FHSEBS~ #8  // small portrait
	WRITE_ASCII 0x3c  ~FHSEBM~ #8  // large portrait
	// Workaround to fix Sebastian creature file installation issue with classic games if TobEx is not installed and there are more and 256 kits installed (I know, pretty rare case, but who knows...)
	PATCH_IF kits_lim_256 BEGIN
		WRITE_SHORT 0x244 0
		WRITE_SHORT 0x246 0x4000 + ~%FHLPLAY%~ // the internal name of the kit, surrounded by %
	END ELSE BEGIN
		WRITE_SHORT 0x244 (playwright_kit_id >> 16) & 0xffff
		WRITE_SHORT 0x246 playwright_kit_id & 0xffff 
	END
	SAY INITIAL_MEETING   ~~
	SAY MORALE            @84  // ~There's nothing cowardly about self-preservation!~ [FHLSS1]
	SAY HAPPY             @85  // ~Oh, I knew I'd have good times with you.~ [FHLSS2]
	SAY UNHAPPY_ANNOYED   @86  // ~I'm older and wiser than you, so why aren't we doing what I want?~ [FHLSS4]
	SAY UNHAPPY_SERIOUS   @87  // ~I'll name no names, but someone in this group needs a kick in the face...~ [FHLSS5]
	SAY UNHAPPY_BREAKING  @88  // ~I don't owe you anything. I'm off.~ [FHLSS6]
	SAY LEADER            @89  // ~All will submit to me!~ [FHLSS7]
	SAY TIRED             @90  // ~I insist we stop. My feet are killing me.~ [FHLSS8]
	SAY BORED             @91  // ~There are so many better things we could be doing. Shall I show you some?~ [FHLSS9]
	SAY BATTLE_CRY1       @92  // ~I'm sure this can be sorted out amicably!~ [FHLSS10]
	SAY BATTLE_CRY2       @93  // ~What are you waiting for? Attack them!~ [FHLSS11]
	SAY BATTLE_CRY3       @94  // ~You there! You look tense! How about a massage?~ [FHLSS12]
	SAY BATTLE_CRY4       @95  // ~Hit 'em in the groin, hit 'em in the knee, hit 'em in the eye so they can't see!~ [FHLSS13]
	SAY BATTLE_CRY5       @96  // ~You fight, I'll, er, guard the packs!~ [FHLSS14]
	SAY DAMAGE            @97  // ~~ [FHLSS15]
	SAY DYING             @98  // ~~ [FHLSS16]
	SAY HURT              @99  // ~Ohh, I'm going to have to wash that out...~ [FHLSS17]
	SAY AREA_FOREST       @100 // ~What a charming setting for a play. Now, where could I find some nymphs?~ [FHLSS18]
	SAY AREA_CITY         @101 // ~Don't worry, I can protect you from the perils of the big city.~ [FHLSS19]
	SAY AREA_DUNGEON      @102 // ~My, you have excellent taste in places to visit. What next? A dank hole? Oh, wait...~ [FHLSS20]
	SAY AREA_DAY          @103 // ~Do we have to adventure today? Can't we just, you know, amuse ourselves?~ [FHLSS21]
	SAY AREA_NIGHT        @104 // ~Ahh, the sky's so clear, the air's so pure, the stars are plentiful.~ [FHLSS22]
	SAY SELECT_COMMON1    @105 // ~Yes?~ [FHLSS23]
	SAY SELECT_COMMON2    @106 // ~At your service.~ [FHLSS24]
	SAY SELECT_COMMON3    @107 // ~Oh, ask someone else.~ [FHLSS25]
	SAY SELECT_COMMON4    @108 // ~I hope you're not about to order me around.~ [FHLSS26]
	SAY SELECT_COMMON5    @109 // ~Can it wait?~ [FHLSS27]
	SAY SELECT_COMMON6    @110 // ~What's wrong with you?~ [FHLSS28]
	SAY SELECT_ACTION1    @111 // ~If I must.~ [FHLSS29]
	SAY SELECT_ACTION2    @112 // ~With pleasure.~ [FHLSS30]
	SAY SELECT_ACTION3    @113 // ~I'm sure you'd do it better.~ [FHLSS31]
	SAY SELECT_ACTION4    @114 // ~Anything for you.~ [FHLSS32]
	SAY SELECT_ACTION5    @115 // ~Only if I get my reward.~ [FHLSS33]
	SAY SELECT_ACTION6    @116 // ~I'd better enjoy this.~ [FHLSS34]
	SAY SELECT_ACTION7    @117 // ~This isn't going to be boring, is it?~ [FHLSS35]
	SAY SELECT_RARE1      @118 // ~Hang on, I'm halfway through a stanza.~ [FHLSS36]
	SAY SELECT_RARE2      @119 // ~Would you like Uncle Sebastian to get you some candy? Maybe then I'll get some peace.~ [FHLSS37]
	SAY CRITICAL_HIT      @120 // ~Did you see that?!~ [FHLSS38]
	SAY CRITICAL_MISS     @121 // ~Meant to do it!~ [FHLSS39]
	SAY TARGET_IMMUNE     @122 // ~I may as well read a book instead.~ [FHLSS40]
	SAY INVENTORY_FULL    @123 // ~I'm not a mule.~ [FHLSS41]
	SAY PICKED_POCKET     @124 // ~Ooh, pretty things for me. I mean...for all of us.~ [FHLSS42]
	SAY HIDDEN_IN_SHADOWS @125 // ~Boo!~ [FHLSS43]
	SAY SPELL_DISRUPTED   @126 // ~I didn't want to cast that one anyway.~ [FHLSS44]
	SAY SET_A_TRAP        @113 // ~I'm sure you'd do it better.~ [FHLSS31]
	SAY BIO               @127 // ~It is fair to say that whenever you question SEBASTIAN about himself he is maddeningly elusive. He is a playwright by trade - or so he says, though he is secretive about his work and claims that a great deal of what he writes would go over your head. He has a keen sense of frivolity and seems to take little in life with any seriousness. Nevertheless, on occasion it appears that there is something else going on with him, and it would be unwise to underestimate his intelligence.~
	WRITE_ASCII SCRIPT_OVERRIDE ~FHLSEB~ #8  // override
	WRITE_ASCII DIALOG          ~FHLSEB~ #8  // dialogue
	WRITE_ASCII DEATHVAR        ~FHLSEB~ #32
	ADD_CRE_ITEM ~STAF01~  #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~
	ADD_CRE_ITEM ~FHLBOOK~ #0 #0 #0 ~IDENTIFIED&UNDROPPABLE~ ~INV1~
	ADD_CRE_ITEM ~POTN52~  #3 #0 #0 ~IDENTIFIED~ ~INV2~
BUT_ONLY

COPY ~%MOD_FOLDER%/cre/fhlcat.cre~ ~override~
	SAY NAME1 @82
	SAY NAME2 @82

// Andrei
// ------
COPY ~%MOD_FOLDER%/cre/fhland07.cre~ ~override~
     ~%MOD_FOLDER%/cre/fhland09.cre~ ~override~
     ~%MOD_FOLDER%/cre/fhland11.cre~ ~override~
     ~%MOD_FOLDER%/cre/fhland13.cre~ ~override~
	SAY NAME1 @130
	SAY NAME2 @130
	WRITE_ASCII 0x34 ~FHANDRES~ #8 // small portrait
	WRITE_ASCII 0x3c ~FHANDREM~ #8 // large portrait
	SAY MORALE            @131 // ~I don't want-- I should go back!~ [FHLASS1]
	SAY HAPPY             @132 // ~Xavier would be proud of what we're doing.~ [FHLASS2]
	SAY UNHAPPY_ANNOYED   @133 // ~What are you doing?~ [FHLASS3]
	SAY UNHAPPY_SERIOUS   @134 // ~I don't know what you think you're doing, but this isn't right.~ [FHLASS4]
	SAY UNHAPPY_BREAKING  @135 // ~Look, that's it.~ [FHLASS5]
	SAY LEADER            @136 // ~I'll do my best to live up to your expectations.~ [FHLASS6]
	SAY TIRED             @137 // ~I don't think it's a good idea for us to get too tired.~ [FHLASS7]
	SAY BORED             @138 // ~I'd rather be in the estate.~ [FHLASS8]
	SAY BATTLE_CRY1       @139 // ~Hyah!~ [FHLASS9]
	SAY BATTLE_CRY2       @140 // ~Die!~ [FHLASS10]
	SAY BATTLE_CRY3       @141 // ~To the death!~ [FHLASS11]
	SAY BATTLE_CRY4       @142 // ~Take that!~ [FHLASS12]
	SAY BATTLE_CRY5       @143 // ~Attack!~ [FHLASS13]
	SAY DAMAGE            @144 // ~~ [FHLASS14]
	SAY DYING             @145 // ~Uh - I wish I could--~ [FHLASS15]
	SAY HURT              @146 // ~I need some healing, quickly!~ [FHLASS16]
	SAY AREA_FOREST       @147 // ~Er... you have got a map, haven't you?~ [FHLASS17]
	SAY AREA_CITY         @148 // ~I never thought I'd see so many people...~ [FHLASS18]
	SAY AREA_DUNGEON      @149 // ~Don't worry, I'm watching your back.~ [FHLASS19]
	SAY AREA_DAY          @150 // ~The sunshine always looks so bright at home.~ [FHLASS20]
	SAY AREA_NIGHT        @151 // ~Are we going to sleep anytime soon?~ [FHLASS21]
	SAY SELECT_COMMON1    @152 // ~Yes?~ [FHLASS22]
	SAY SELECT_COMMON2    @153 // ~Hmm?~ [FHLASS23]
	SAY SELECT_COMMON3    @154 // ~What is it?~ [FHLASS24]
	SAY SELECT_COMMON4    @155 // ~Do you need anything?~ [FHLASS25]
	SAY SELECT_COMMON5    @156 // ~What?~ [FHLASS26]
	SAY SELECT_COMMON6    @157 // ~What do you need?~ [FHLASS27]
	SAY SELECT_ACTION1    @158 // ~All right.~ [FHLASS28]
	SAY SELECT_ACTION2    @159 // ~Of course.~ [FHLASS29]
	SAY SELECT_ACTION3    @160 // ~Whatever you say.~ [FHLASS30]
	SAY SELECT_ACTION4    @161 // ~If it helps.~ [FHLASS31]
	SAY SELECT_ACTION5    @162 // ~Sure.~ [FHLASS32]
	SAY SELECT_ACTION6    @163 // ~If that's what you want.~ [FHLASS33]
	SAY SELECT_ACTION7    @164 // ~If you say so.~ [FHLASS34]
	SAY SELECT_RARE1      @165 // ~Andrei Turi Xavier Luxley, at your service.~ [FHLASS35]
	SAY SELECT_RARE2      @166 // ~As eventful as all this is, I do miss the estate.~ [FHLASS36]
	SAY CRITICAL_HIT      @167 // ~Ha!~ [FHLASS37]
	SAY CRITICAL_MISS     @168 // ~Dammit!~ [FHLASS38]
	SAY TARGET_IMMUNE     @169 // ~I can't seem to do anything!~ [FHLASS39]
	SAY INVENTORY_FULL    @170 // ~I can't carry all of that.~ [FHLASS40]
	SAY PICKED_POCKET     ~~
	SAY HIDDEN_IN_SHADOWS @171 // ~Maybe I should get a hood.~ [FHLASS41]
	SAY SPELL_DISRUPTED   @172 // ~Ah, dammit!~ [FHLASS42]
	SAY SET_A_TRAP        ~~
	SAY BIO               @173 // ~ANDREI smiles when you ask him about himself. Though he is willing to talk about his relatives and is forthright about his beliefs, he is politely evasive on some subjects. He is skilled in hand-to-hand combat, a skill he developed at a young age, and was trained by an older member of his family. Despite his combat expertise, Andrei has only just left the family estate and seems a little naive about certain aspects of everyday life that you, perhaps, take for granted. Nevertheless, he comes across as quietly confident and it is rare to see him stray from a path he has chosen.~
	WRITE_ASCII SCRIPT_OVERRIDE ~FHLAND~ #8
	WRITE_ASCII DIALOG          ~FHLAND~ #8
	WRITE_ASCII DEATHVAR        ~FHLAND~ #32
	ADD_CRE_ITEM ~AMUL14~ #0 #0 #0 ~IDENTIFIED~ ~AMULET~
	ADD_CRE_ITEM ~POTN52~ #2 #0 #0 ~IDENTIFIED~ ~INV1~

COPY ~%MOD_FOLDER%/cre/fhlbear.cre~ ~override~
	SAY NAME1 @130
	SAY NAME2 @130

// Alexandra
// ---------
COPY ~%MOD_FOLDER%/cre/fhlal.cre~ ~override~
	SAY NAME1 @181
	SAY NAME2 @181
	ADD_CRE_ITEM ~HELMNOAN~ #0 #0 #0 ~NONE~ ~HELMET~
	ADD_CRE_ITEM ~CLCK13~   #0 #0 #0 ~NONE~ ~ARMOR~
	ADD_CRE_ITEM ~BRAC02~   #0 #0 #0 ~NONE~ ~GLOVES~
	ADD_CRE_ITEM ~POTN52~   #4 #0 #0 ~NONE~ ~INV1~
	ADD_CRE_ITEM ~WAND10~   #0 #0 #0 ~NONE~ ~INV2~
	ADD_CRE_ITEM ~SCRL71~   #0 #0 #0 ~NONE~ ~INV5~
	ADD_CRE_ITEM ~RNDTRE04~ #0 #0 #0 ~NONE~ ~INV6~
	ADD_CRE_ITEM ~RNDTRE05~ #0 #0 #0 ~NONE~ ~INV7~

// Alyssia
// -------
COPY ~%MOD_FOLDER%/cre/fhlali.cre~ ~override~
	SAY NAME1 @182
	SAY NAME2 @182

// Archie
// ------
COPY ~%MOD_FOLDER%/cre/fhlarch.cre~ ~override~
	SAY NAME1 @183
	SAY NAME2 @183

// Robin
// -----
COPY ~%MOD_FOLDER%/cre/fhlboy.cre~ ~override~
	SAY NAME1 @184
	SAY NAME2 @184

// Thomas
// ------
COPY ~%MOD_FOLDER%/cre/fhlboy2.cre~ ~override~
	SAY NAME1 @185
	SAY NAME2 @185

// Boy
// ---
COPY ~%MOD_FOLDER%/cre/fhlboy3.cre~ ~override~
	SAY NAME1 @186
	SAY NAME2 @186

// Brid
// ----
COPY ~%MOD_FOLDER%/cre/fhlbrid.cre~ ~override~
	SAY NAME1 @187
	SAY NAME2 @187
	ADD_CRE_ITEM ~RNDTRE01~ #0 #0 #0 ~NONE~ ~QITEM1~

// Edward
// ------
COPY ~%MOD_FOLDER%/cre/fhlchick.cre~ ~override~
	SAY NAME1 @188
	SAY NAME2 @188

// Mugger
// ------
COPY ~%MOD_FOLDER%/cre/fhlcrim1.cre~ ~override~
	SAY NAME1 @189
	SAY NAME2 @189

// Durydil
// -------
COPY ~%MOD_FOLDER%/cre/fhldu.cre~ ~override~
	SAY NAME1 @190
	SAY NAME2 @190

// Commoner
// --------
COPY ~%MOD_FOLDER%/cre/fhlfred.cre~ ~override~
	SAY NAME1 @191
	SAY NAME2 @191

// Genevieve
// ---------
COPY ~%MOD_FOLDER%/cre/fhlgen.cre~ ~override~
	SAY NAME1 @192
	SAY NAME2 @192
	SAY SELECT_COMMON1 @193 // ~Hello, there.~ [FHLG4]

// Clara
// -----
COPY ~%MOD_FOLDER%/cre/fhlgirl.cre~ ~override~
	SAY NAME1 @194
	SAY NAME2 @194

// Lianne
// ---------
COPY ~%MOD_FOLDER%/cre/fhlgirl2.cre~ ~override~
	SAY NAME1 @195
	SAY NAME2 @19

// Josefyne
// ---------
COPY ~%MOD_FOLDER%/cre/fhljos.cre~ ~override~
	SAY NAME1 @196
	SAY NAME2 @196
	ADD_CRE_ITEM ~STAF18~   #0 #0 #0 ~NONE~ ~WEAPON1~
	ADD_CRE_ITEM ~HELMNOAN~ #0 #0 #0 ~NONE~ ~HELMET~
	ADD_CRE_ITEM ~POTN52~   #2 #0 #0 ~IDENTIFIED~ ~QITEM1~
	ADD_CRE_ITEM ~SCRL71~   #0 #0 #0 ~IDENTIFIED~ ~INV5~
	ADD_CRE_ITEM ~RNDTRE04~ #0 #0 #0 ~NONE~ ~INV6~
	ADD_CRE_ITEM ~RNDTRE05~ #0 #0 #0 ~NONE~ ~INV7~
	ADD_CRE_ITEM ~MINHP1~   #0 #0 #0 ~NONE~ ~AMULET~

// Kavyn
// -----
COPY ~%MOD_FOLDER%/cre/fhlkav.cre~ ~override~
	SAY NAME1 @197
	SAY NAME2 @19

// Lealia
// ------
COPY ~%MOD_FOLDER%/cre/fhlleal.cre~ ~override~
	SAY NAME1 @198
	SAY NAME2 @198
	SAY SELECT_COMMON1 @199 // ~Hello, there. Is there anything you need?~ [FHLLEA6]
	ADD_CRE_ITEM ~CHAN01~ #0 #0 #0 ~NONE~ ~ARMOR~
	ADD_CRE_ITEM ~POTN52~ #3 #0 #0 ~NONE~ ~QITEM1~
	ADD_CRE_ITEM ~BLUN03~ #0 #0 #0 ~NONE~ ~WEAPON1~
	ADD_CRE_ITEM ~SHLD04~ #0 #0 #0 ~NONE~ ~SHIELD~

// Lucas
// -----
COPY ~%MOD_FOLDER%/cre/fhlluc.cre~ ~override~
	SAY NAME1 @200
	SAY NAME2 @200
	SAY SELECT_COMMON1 @201 // ~What do you think you're looking at?~ [FHLLUC3]
	ADD_CRE_ITEM ~POTN52~   #3 #0 #0 ~NONE~ ~INV1~
	ADD_CRE_ITEM ~POTN11~   #0 #0 #0 ~NONE~ ~INV2~
	ADD_CRE_ITEM ~RNDTRE02~ #0 #0 #0 ~NONE~ ~INV6~

// Lyel
// ----
COPY ~%MOD_FOLDER%/cre/fhllyel.cre~ ~override~
	SAY NAME1 @202
	SAY NAME2 @202
	ADD_CRE_ITEM ~CHAN05~ #0 #0 #0 ~NONE~ ~ARMOR~

COPY ~%MOD_FOLDER%/cre/fhllyel2.cre~ ~override~
	SAY NAME1 @202
	SAY NAME2 @202
	ADD_CRE_ITEM ~CHAN05~ #0 #0 #0 ~NONE~ ~ARMOR~
	ADD_CRE_ITEM ~SHLD04~ #0 #0 #0 ~NONE~ ~SHIELD~
	ADD_CRE_ITEM ~SW1H14~ #0 #0 #0 ~NONE~ ~WEAPON1~

// Alistair
// --------
COPY ~%MOD_FOLDER%/cre/fhlm1.cre~ ~override~
	SAY NAME1 @203
	SAY NAME2 @203
	ADD_CRE_ITEM ~RNDTRE01~ #0 #0 #0 ~NONE~ ~QITEM1~

// Lantro
// ------
COPY ~%MOD_FOLDER%/cre/fhlm2.cre~ ~override~
	SAY NAME1 @204
	SAY NAME2 @204
	ADD_CRE_ITEM ~RNDTRE01~ #0 #0 #0 ~NONE~ ~QITEM1~

// Jonathen
// --------
COPY ~%MOD_FOLDER%/cre/fhlm3.cre~ ~override~
	SAY NAME1 @205
	SAY NAME2 @205
	ADD_CRE_ITEM ~RNDTRE01~ #0 #0 #0 ~NONE~ ~QITEM1~

// Katrin
// ------
COPY ~%MOD_FOLDER%/cre/fhlm4.cre~ ~override~
	SAY NAME1 @205
	SAY NAME2 @206
	ADD_CRE_ITEM ~RNDTRE01~ #0 #0 #0 ~NONE~ ~QITEM1~

// Nicolai
// -------
COPY ~%MOD_FOLDER%/cre/fhlm5.cre~ ~override~
	SAY NAME1 @207
	SAY NAME2 @207
	ADD_CRE_ITEM ~RNDTRE01~ #0 #0 #0 ~NONE~ ~QITEM1~

// Farris
// ------
COPY ~%MOD_FOLDER%/cre/fhlm6.cre~ ~override~
	SAY NAME1 @208
	SAY NAME2 @208
	ADD_CRE_ITEM ~RNDTRE01~ #0 #0 #0 ~NONE~ ~QITEM1~

// Tiger
// -----
COPY ~%MOD_FOLDER%/cre/fhlmie1.cre~ ~override~
	SAY NAME1 @209
	SAY NAME2 @209

// Mietta
// ------
COPY ~%MOD_FOLDER%/cre/fhlmiet.cre~ ~override~
	SAY NAME1 @55
	SAY NAME2 @55

// Miles
// -----
COPY ~%MOD_FOLDER%/cre/fhlmil.cre~ ~override~
	SAY NAME1 @210
	SAY NAME2 @210

// Katya
// -----
COPY ~%MOD_FOLDER%/cre/fhlmw.cre~ ~override~
	SAY NAME1 @211
	SAY NAME2 @211

// Nari
// ----
COPY ~%MOD_FOLDER%/cre/fhlngrl.cre~ ~override~
	SAY NAME1 @212
	SAY NAME2 @212

// Quennell
// --------
COPY ~%MOD_FOLDER%/cre/fhlquen.cre~ ~override~
	SAY NAME1 @213
	SAY NAME2 @213
	ADD_CRE_ITEM ~FHLCHR~ #0 #0 #0 ~NONE~ ~INV6~

// Rosanna
// -------
COPY ~%MOD_FOLDER%/cre/fhlros.cre~ ~override~
	SAY NAME1 @214
	SAY NAME2 @214
	SAY SELECT_COMMON1 @215 // ~Hello again.~ [FHLROS5]
	ADD_CRE_ITEM ~RNDTRE02~ #0 #0 #0 ~NONE~ ~INV6~
	ADD_CRE_ITEM ~POTN52~   #3 #0 #0 ~NONE~ ~INV1~
	ADD_CRE_ITEM ~POTN11~   #0 #0 #0 ~NONE~ ~INV2~

// Samuel
// ------
COPY ~%MOD_FOLDER%/cre/fhlsam.cre~ ~override~
	SAY NAME1 @216
	SAY NAME2 @216
	SAY SELECT_COMMON1 @217 // ~Hello again.~ [FHLSAM2]
	ADD_CRE_ITEM ~PLAT04~ #0 #0 #0 ~IDENTIFIED~ ~ARMOR~
	ADD_CRE_ITEM ~SHLD05~ #0 #0 #0 ~IDENTIFIED~ ~SHIELD~
	ADD_CRE_ITEM ~HAMM03~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~

// Simeon
// ------
COPY ~%MOD_FOLDER%/cre/fhlsim.cre~ ~override~
	SAY NAME1 @218
	SAY NAME2 @218
	ADD_CRE_ITEM ~MFIST5~   #0 #0 #0 ~NONE~ ~WEAPON1~
	ADD_CRE_ITEM ~POTN52~   #3 #0 #0 ~NONE~ ~INV1~
	ADD_CRE_ITEM ~POTN11~   #0 #0 #0 ~NONE~ ~INV2~
	ADD_CRE_ITEM ~RNDTRE02~ #0 #0 #0 ~NONE~ ~INV6~

// Skeleton Warrior
// ----------------
COPY ~%MOD_FOLDER%/cre/fhlskl1.cre~ ~override~
     ~%MOD_FOLDER%/cre/fhlskl2.cre~ ~override~
	SAY NAME1 @219
	SAY NAME2 @219

// Turi
// ----
COPY ~%MOD_FOLDER%/cre/fhlturi.cre~ ~override~
	SAY NAME1 @220
	SAY NAME2 @220
	ADD_CRE_ITEM ~RNDTRE01~ #0 #0 #0 ~NONE~ ~QITEM1~

// Katryna
// -------
COPY ~%MOD_FOLDER%/cre/fhlw1.cre~ ~override~
	SAY NAME1 @221
	SAY NAME2 @221
	ADD_CRE_ITEM ~LEAT02~   #0 #0 #0 ~NONE~ ~ARMOR~
	ADD_CRE_ITEM ~RNDTRE01~ #0 #0 #0 ~NONE~ ~QITEM1~

// Siri
// ----
COPY ~%MOD_FOLDER%/cre/fhlw2.cre~ ~override~
	SAY NAME1 @222
	SAY NAME2 @222
	ADD_CRE_ITEM ~CHAN01~   #0 #0 #0 ~NONE~ ~ARMOR~
	ADD_CRE_ITEM ~RNDTRE01~ #0 #0 #0 ~NONE~ ~QITEM1~

// Danica
// ------
COPY ~%MOD_FOLDER%/cre/fhlw3.cre~ ~override~
	SAY NAME1 @223
	SAY NAME2 @223
	ADD_CRE_ITEM ~RNDTRE01~ #0 #0 #0 ~NONE~ ~QITEM1~

// Erinne
// ------
COPY ~%MOD_FOLDER%/cre/fhlw4.cre~ ~override~
	SAY NAME1 @224
	SAY NAME2 @224
	ADD_CRE_ITEM ~RNDTRE01~ #0 #0 #0 ~NONE~ ~QITEM1~

// Serah
// -----
COPY ~%MOD_FOLDER%/cre/fhlw5.cre~ ~override~
	SAY NAME1 @225
	SAY NAME2 @225
	ADD_CRE_ITEM ~RNDTRE01~ #0 #0 #0 ~NONE~ ~QITEM1~

// Xavier
// ------
COPY ~%MOD_FOLDER%/cre/fhlxav.cre~ ~override~
	SAY NAME1 @226
	SAY NAME2 @226
	ADD_CRE_ITEM ~HELMNOAN~ #0 #0 #0 ~NONE~ ~HELMET~
	ADD_CRE_ITEM ~CLCK10~   #0 #0 #0 ~NONE~ ~ARMOR~
	ADD_CRE_ITEM ~RING06~   #0 #0 #0 ~NONE~ ~LRING~
	ADD_CRE_ITEM ~POTN52~   #4 #0 #0 ~IDENTIFIED~ ~QITEM1~
	ADD_CRE_ITEM ~RNDTRE04~ #0 #0 #0 ~NONE~ ~INV6~
	ADD_CRE_ITEM ~RNDTRE05~ #0 #0 #0 ~NONE~ ~INV7~


/* ------------------------------------------------- *
 *  Appending Sebastian and Andrei's dialogue files  *
 * ------------------------------------------------- */

PRINT @231 // ~Appending Sebastian and Andrei's dialogue files to the game...~

// BG2 game without ToB
// --------------------
APPEND ~pdialog.2da~ ~FHLSEB FHLSEBP FHLSEBJ FHLSEBD~ UNLESS ~FHLSEB~ UNLESS ~25POST~
APPEND ~pdialog.2da~ ~FHLAND FHLANDP FHLANDJ FHLANDD~ UNLESS ~FHLAND~ UNLESS ~25POST~

APPEND ~interdia.2da~ ~FHLSEB BFHLSEB~ UNLESS ~FHLSEB~ UNLESS ~25POST~
APPEND ~interdia.2da~ ~FHLAND BFHLAND~ UNLESS ~FHLAND~ UNLESS ~25POST~

// ToB based game
// --------------
APPEND ~pdialog.2da~ ~FHLSEB FHLSEBP FHLSEBJ FHLSEBD FHLS25P FHLS25J FHLS25D FHLS25~ UNLESS ~FHLSEB~ IF ~25POST~
APPEND ~pdialog.2da~ ~FHLAND FHLANDP FHLANDJ FHLANDD FHLA25P FHLA25J FHLA25D FHLA25~ UNLESS ~FHLAND~ IF ~25POST~

APPEND ~interdia.2da~ ~FHLSEB BFHLSEB BFHSEB25~ UNLESS ~FHLSEB~ IF ~25POST~
APPEND ~interdia.2da~ ~FHLAND BFHLAND BFHLAND~ UNLESS ~FHLAND~ IF ~25POST~
